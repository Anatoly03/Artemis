def profiles = "prod"

if (project.hasProperty("api-docs")) {
    profiles += ",api-docs"
}

springBoot {
    buildInfo()
}

bootRun {
    args = []
}

task npm_install(type: NpmTask) {
    workingDir = file("../")
    args = ['install']
}

task webapp_test(type: NpmTask, dependsOn: "npm_install") {
    workingDir = file("../")
    args = [ "run", "webapp:test"]
}

task webapp(type: NpmTask, dependsOn: "npm_install") {
    workingDir = file("../")
    args = ["run", "webapp:prod"]
    environment = [APP_VERSION: project.version]
}

task copyClientBuildFiles(type: Copy) {
    from "../build/resources/main/"
    into "build/resources/main/"
    exclude "config"
    fileMode = 0644
}

processResources {
    inputs.property('version', version)
    inputs.property('springProfiles', profiles)
    filesMatching("**/application.yml") {
        filter {
            it.replace("#project.version#", version)
        }
    }
    filesMatching("**/bootstrap.yml") {
        filter {
            it.replace("#spring.profiles.active#", profiles)
        }
    }
}

test.dependsOn webapp_test
tasks.copyClientBuildFiles.dependsOn webapp
processResources.dependsOn tasks.copyClientBuildFiles
bootJar.dependsOn processResources
